plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

dependencies {
    // Gradle API
    implementation gradleApi()
    
    // Patch core library
    implementation project(':patch-core')
    
    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation gradleTestKit()
}

gradlePlugin {
    plugins {
        patchPlugin {
            id = 'com.orange.patch'
            implementationClass = 'com.orange.patch.plugin.PatchPlugin'
            displayName = 'Patch Generator Plugin'
            description = 'Gradle plugin for generating hot update patches for Android applications'
        }
    }
}

// Publishing configuration
publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = 'com.orange.patch'
            artifactId = 'patch-gradle-plugin'
            version = '1.2.8'
        }
    }
    repositories {
        maven {
            name = 'local'
            url = layout.buildDirectory.dir('repo')
        }
    }
}

test {
    useJUnit()
}
